plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }

    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

neoForge {
    neoFormVersion = neoformTimestamp

    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if ( at.exists() )
        accessTransformer.add at.absolutePath

    parchment {
        minecraftVersion = parchmentMc
        mappingsVersion = parchmentVersion
    }
}

dependencies {
    compileOnly 'org.spongepowered:mixin:0.8.5'

    compileOnly 'io.github.llamalad7:mixinextras-common:0.3.5'
    annotationProcessor 'io.github.llamalad7:mixinextras-common:0.3.5'
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}