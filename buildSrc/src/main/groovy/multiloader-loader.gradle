plugins {
    id 'multiloader-common'
}

configurations {

    commonJava {
        canBeResolved = true
    }

    commonResources {
        canBeResolved = true
    }

}

dependencies {
    compileOnly(project(":XPlat")) {
        capabilities {
            requireCapability("$group:$modId")
        }
    }

    commonJava project(path: ":XPlat", configuration: 'commonJava')
    commonResources project(path: ':XPlat', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn (configurations.commonJava)
    source (configurations.commonJava)
}

processResources {
    dependsOn (configurations.commonResources)
    from (configurations.commonResources)
}

tasks.named('sourcesJar', Jar) {
    dependsOn (configurations.commonJava)
    from (configurations.commonJava)
    dependsOn (configurations.commonResources)
    from (configurations.commonResources)
}