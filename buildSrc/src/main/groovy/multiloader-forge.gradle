plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle'
}

dependencies {
    minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"

    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

minecraft {
    mappings channel: 'official', version: "${mcVersion}"
    reobf = false
    copyIdeResources = true

    def at = project(":XPlat").file('src/main/resources/META-INF/accesstransformer.cfg')
    if ( at.exists() ) accessTransformer = file(at.absolutePath)

    runs {
        client {
            property 'forge.enabledGameTestNamespaces', "${modId}"
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }

        server {
            property 'forge.enabledGameTestNamespaces', "${modId}"
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.forEach {
    def dir = layout.buildDirectory.dir("sourceSets/${it.name}")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
