pluginManagement {
    repositories {
        gradlePluginPortal ()
        mavenCentral ()

        exclusiveContent {
            forRepository {
                maven {
                    name = 'FabricMaven'
                    url = uri('https://maven.fabricmc.net/')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = 'ForgeMaven'
                    url = uri('https://maven.minecraftforge.net/')
                }
            }
            filter {
                includeGroupAndSubgroups('net.minecraftforge')
            }
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = 'NeoMaven'
                    url = uri('https://maven.neoforged.net/releases/')
                }
            }
            filter {
                includeGroupByRegex("net\\.neoforged.*")
            }
        }
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "net.minecraftforge.gradle") {
                useModule("${requested.id}:ForgeGradle:${requested.version}")
            }
            if (requested.id.id == "org.spongepowered.mixin") {
                useModule("org.spongepowered:mixingradle:${requested.version}")
            }
        }
    }

}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'WgcCore'
include 'XPlat'
include 'Fabric'
include 'Neoforge'

