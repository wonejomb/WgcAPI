pluginManagement {
    repositories {
        gradlePluginPortal ()
        mavenCentral ()

        exclusiveContent {
            forRepository {
                maven {
                    name = 'ForgeMaven'
                    url = uri('https://maven.minecraftforge.net/')
                }
            }
            filter {
                includeGroupByRegex("net\\.minecraftforge.*")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'ParchmentMaven'
                    url = uri('https://maven.parchmentmc.org')
                }
            }
            filter {
                includeGroupByRegex("org\\.parchmentmc.*")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'FabricMaven'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'NeoforgedMaven'
                    url = uri('https://maven.neoforged.net/releases')
                }
            }
            filter {
                includeGroupByRegex("net\\.neoforged.*")
                includeGroup('codechicken')
                includeGroup('net.covers1624')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public/')
                }
            }
            filter {
                includeGroupByRegex("org\\.spongepowered.*")
            }
        }
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "net.minecraftforge.gradle") {
                useModule("${requested.id}:ForgeGradle:${requested.version}")
            }
            if (requested.id.id == "org.spongepowered.mixin") {
                useModule("org.spongepowered:mixingradle:${requested.version}")
            }
        }
    }

}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'WgcCore'

include 'XPlat'
include 'Fabric'
include 'Neoforge'
include 'Forge'

